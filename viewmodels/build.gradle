plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '2.0'
    id 'mirego.publish' version '1.0'
    id 'kotlin-kapt'
}

repositories {
    google()
    mavenLocal()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kotlin {
    android {
        publishAllLibraryVariants()
    }

    jvm()
    ios()
    iosArm32('iosArm32')
    tvos()

    js(IR) {
        browser()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "com.mirego.trikot:streams:$trikot_streams_version"
                implementation "com.mirego.trikot:trikotFoundation:$trikot_foundation_version"
            }
        }

        jvmMain {
            dependsOn commonMain
        }

        androidMain {
            dependencies {
                implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
                implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
                implementation 'androidx.recyclerview:recyclerview:1.1.0'
                implementation 'androidx.appcompat:appcompat:1.2.0'
                implementation 'com.squareup.picasso:picasso:2.71828'
                implementation 'com.google.android.material:material:1.3.0'
                implementation 'javax.annotation:javax.annotation-api:1.3.2'
            }

            dependsOn commonMain
        }

        jsMain {
            dependsOn commonMain
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        nativeMain {
            dependsOn commonMain
        }

        nativeTest {
            dependsOn commonTest
            iosX64Test.dependsOn(it)
            iosArm64Test.dependsOn(it)
            tvosArm64Test.dependsOn(it)
            tvosX64Test.dependsOn(it)
            iosArm32Test.dependsOn(it)
        }

        iosMain {
            dependsOn nativeMain
            iosX64Main.dependsOn(it)
            iosArm64Main.dependsOn(it)
        }

        iosArm32Main {
            dependsOn nativeMain
        }

        tvosMain {
            dependsOn nativeMain
            tvosArm64Main.dependsOn(it)
            tvosX64Main.dependsOn(it)
        }
    }
}

dependencies {
    kapt "com.android.databinding:compiler:3.1.4"
}

release {
   checkTasks = ['check']
   buildTasks = ['publish']
   updateVersionPart = 2
}
