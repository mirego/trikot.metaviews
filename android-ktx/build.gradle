plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-platform-android'
    id 'digital.wup.android-maven-publish' version '3.6.2'
    id 'mirego.release' version '2.0'
    id 'mirego.publish' version '1.0'
    id 'kotlin-kapt'
}

repositories {
    mavenLocal()
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot.viewmodels'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }

    dataBinding {
        enabled = true
    }
}

configurations.all {
    exclude group: "org.reactivestreams"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'com.mirego.trikot:viewmodels:1.0.9-SNAPSHOT'
    implementation "com.mirego.trikot:android-ktx:$trikot_streams_android_ktx_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Add missing dependencies for JDK 9+
    if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
        // If you're using @AutoValue or any libs that requires javax.annotation (like Dagger)
        compileOnly 'com.github.pengrad:jdk9-deps:1.0'
        compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

        // If you're using Kotlin
        kapt "com.sun.xml.bind:jaxb-core:2.3.0.1"
        kapt "javax.xml.bind:jaxb-api:2.3.1"
        kapt "com.sun.xml.bind:jaxb-impl:2.3.2"

        // If you're using Java
        annotationProcessor "com.sun.xml.bind:jaxb-core:2.3.0.1"
        annotationProcessor "javax.xml.bind:jaxb-api:2.3.1"

        testAnnotationProcessor "com.sun.xml.bind:jaxb-core:2.3.0.1"
        testAnnotationProcessor "javax.xml.bind:jaxb-api:2.3.1"
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']
    updateVersionPart = 2
    tagPrefix = 'android-ktx-'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            artifact sourcesJar
        }
    }
}
